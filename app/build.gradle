apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "org.proninyaroslav.libretorrent"
        minSdkVersion 15 /* Officially Android Jelly Bean (< 4.4) support is dropped in libtorrent4j */
        targetSdkVersion 28
        versionCode 12
        /*
         * Convection:
         *  major.minor.[revision][-some_info]
         */
        versionName "2.0-beta01"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"

            testCoverageEnabled true
            debuggable true
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    project.ext.versionCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, 'x86': 3, 'x86_64': 4]
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 1000000 + android.defaultConfig.versionCode
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    testOptions.unitTests.includeAndroidResources = true

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

def LIBTORRENT4J_VERSION='1.2.1.0'
def ROOM_VERSION = "2.2.0-rc01"
def ACRA_VERSION = "5.4.0"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    /* Testing */
    testImplementation "junit:junit:4.12"
    testImplementation "org.robolectric:robolectric:4.3"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation ("androidx.room:room-testing:2.1.0") {
        /* The fix for dependency conflict with uk.co.markormesher:android-fab */
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }

    /* AndroidX libraries */
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-beta2"
    implementation "androidx.recyclerview:recyclerview:1.1.0-beta04"
    implementation "androidx.recyclerview:recyclerview-selection:1.1.0-alpha06"
    implementation "com.google.android.material:material:1.1.0-alpha10"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0-alpha02"
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
    annotationProcessor "androidx.room:room-compiler:$ROOM_VERSION" /* For schemas import */
    implementation "androidx.work:work-runtime:2.2.0"
    implementation "androidx.multidex:multidex:2.0.1"

    /* ReactiveX */
    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"

    /* ACRA */
    implementation "ch.acra:acra-mail:$ACRA_VERSION"
    implementation "ch.acra:acra-dialog:$ACRA_VERSION"

    /* libtorrent wrapper */
    implementation "org.libtorrent4j:libtorrent4j:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-arm:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-arm64:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-x86:$LIBTORRENT4J_VERSION"
    implementation "org.libtorrent4j:libtorrent4j-android-x86_64:$LIBTORRENT4J_VERSION"

    /* Other */
    implementation "net.cachapa.expandablelayout:expandablelayout:2.9.2"
    implementation "com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0"
    implementation "com.github.anthonynsimon:jurl:v0.4.2"
    implementation "com.jaredrummler:colorpicker:1.1.0"
    /* Don't use Commons IO >= 2.6, because they uses Java NIO, supported only from API 26 */
    implementation "commons-io:commons-io:2.5"
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "com.takisoft.preferencex:preferencex:1.1.0-alpha05"
    implementation "uk.co.markormesher:android-fab:2.5.0"
    implementation "net.java.dev.jna:jna:5.4.0@aar"
}
