apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.1"

    defaultConfig {
        applicationId "org.proninyaroslav.libretorrent"
        minSdkVersion 15 /* Officially Android Jelly Bean (< 4.4) support is dropped in libtorrent4j */
        targetSdkVersion 28
        versionCode 9
        /*
         * Convection:
         *  major.minor.[revision][-some_info]
         */
        versionName "1.8"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"

            testCoverageEnabled true
            debuggable true
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }

    project.ext.versionCodes = ['armeabi-v7a': 1, 'arm64-v8a': 2, 'x86': 3, 'x86_64': 4]

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 1000000 + android.defaultConfig.versionCode
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
}

def ANDROID_SUPPORT_VERSION='28.0.0-alpha3'
def LIBTORRENT4J_VERSION='1.2.0.20-RC1'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'

    /* Standard Android Support libs */
    implementation 'com.android.support:support-v4:' + ANDROID_SUPPORT_VERSION
    implementation 'com.android.support:appcompat-v7:' + ANDROID_SUPPORT_VERSION
    implementation 'com.android.support:design:' + ANDROID_SUPPORT_VERSION
    implementation 'com.android.support:cardview-v7:' + ANDROID_SUPPORT_VERSION

    /* Don't use Commons IO >= 2.6, because they uses Java NIO, supported only from API 26 */
    implementation 'commons-io:commons-io:2.5'
    implementation 'ch.acra:acra:4.11'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.takisoft.fix:preference-v7:28.0.0.0-alpha3'
    implementation 'com.github.aakira:expandable-layout:1.6.0@aar'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.github.yukuku:ambilwarna:2.0.1'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'net.java.dev.jna:jna:4.5.2'

    /* libtorrent wrapper */
    implementation 'org.libtorrent4j:libtorrent4j:' + LIBTORRENT4J_VERSION
    implementation 'org.libtorrent4j:libtorrent4j-android-arm:' + LIBTORRENT4J_VERSION
    implementation 'org.libtorrent4j:libtorrent4j-android-arm64:' + LIBTORRENT4J_VERSION
    implementation 'org.libtorrent4j:libtorrent4j-android-x86:' + LIBTORRENT4J_VERSION
    implementation 'org.libtorrent4j:libtorrent4j-android-x86_64:' + LIBTORRENT4J_VERSION
}
